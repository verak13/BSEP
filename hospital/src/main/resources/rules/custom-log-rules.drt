template header
name
type
severity
seconds
times
precSec
precTypes
succSec
succTypes
msg

package hospital.hospital.rules;

import hospital.hospital.model.cep.LogEvent;
import hospital.hospital.services.MailService;
import hospital.hospital.model.cep.alarms.CustomLogAlarm;
import java.util.Date;

global MailService mailService

template "custom-log-rules"

rule "@{name}_@{row.rowNumber}"
    when
        $previous: LogEvent(log.getType() in @{precTypes})
        $l1 : LogEvent(
                log.getType() in @{type},
                log.getSeverity().name() in @{severity},
                this != $previous,
                this after[0, @{precSec}] $$previous
                )
        $appearance: Long(intValue >= @{times}) from accumulate(
                                    $l2: LogEvent(
                                        this != $l1,
                                        log.getType() in @{type},
                                        log.getSeverity().name() in @{severity}
                                    )
                                    over window:time(@{seconds}s),
                                    count($l2)
                                )

         $next: LogEvent(
                   log.getType() in @{succTypes},
                   this != $l1,
                   this after[0, @{succSec}] $$l1
                   )
            not (CustomLogAlarm(date == $l1.getLog().getTimestamp()))
    then
       System.out.println("\n\n==== TRIGEROVAN CUSTOM LOG ALARM  ===");
       System.out.println("RULE NAME: " + "@{name}");
       insert(new CustomLogAlarm($l1.getLog().getTimestamp(), "@{msg}"));
end

rule "Custom log alarm triggered, notify admin by email_@{name}_"
  when
        $alarm : CustomLogAlarm($message: errorMsg, $date: date)
    then
        System.out.println("Salje se mejl");
        String msg = "Custom log alarm triggered on date : " + $date + ". Alarm message: " + $message + ".";
        mailService.notifyAdmin($alarm.getAdminEmail(), msg);
end

end template