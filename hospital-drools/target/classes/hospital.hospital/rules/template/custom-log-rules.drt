template header
name
type
severity
seconds
times
precSec
precTypes
succSec
succTypes
msg

package hospital.hospital.rules;

import hospital.hospital.model.cep.LogEvent;
import hospital.hospital.services.MailService;
import hospital.hospital.model.cep.alarms.CustomLogAlarm;
import java.util.Date;

global MailService mailService

template "custom-log-rules"

rule "@{name}_@{row.rowNumber}"
no-loop true
    when
        $previous: LogEvent(log.getType() in @{precTypes})
        $next: LogEvent(log.getType() in @{succTypes}, this != $previous)
        $l1 : LogEvent(
                log.getType() in @{type},
                log.getSeverity().name() in @{severity},
                this != $previous,
                this != $next,
                this after[0, @{precSec}s] $$previous,
                $next after[0, @{succSec}s] this
                )
        $appearance: Long(intValue >= @{times}) from accumulate(
                                    $l2: LogEvent(
                                        this != $l1,
                                        log.getType() in @{type},
                                        log.getSeverity().name() in @{severity}
                                    )
                                    over window:time(@{seconds}s),
                                    count($l2)
                                )


               not (CustomLogAlarm(ruleName == "@{name}"))
    then
       System.out.println("\n\n==== TRIGEROVAN CUSTOM LOG ALARM  ===");
       System.out.println("RULE NAME: " + "@{name}");
       insert(new CustomLogAlarm($l1.getLog().getTimestamp(), "@{msg}", "@{name}"));
end

rule "Custom log alarm triggered, notify admin by email_@{name}_"
no-loop true
  when
        $alarm : CustomLogAlarm($message: errorMsg, $date: date, sentEmail == false)
    then
        System.out.println("\n\nSENDING EMAIL...");
        String msg = "Custom log alarm triggered on date : " + $date + ". Alarm message: " + $message + ".Rule name: " + $alarm.getRuleName();
        mailService.notifyAdmin($alarm.getAdminEmail(), msg);
        mailService.saveCustomAlarm($alarm);
        modify($alarm) { setSentEmail(true); }

end

end template